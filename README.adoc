= Jenkins Pipeline Shared Library Gradle Plugin
:toc:
:latest-version: 0.2.0
:uri-github-releases: https://github.com/mkobit/jenkins-pipeline-shared-libraries-gradle-plugin/releases
:uri-jenkins-test-harness: https://github.com/jenkinsci/jenkins-test-harness.git
:uri-jenkins-shared-library-docs: https://jenkins.io/doc/book/pipeline/shared-libraries/
:uri-jenkins-pipeline-unit: https://github.com/lesfurets/JenkinsPipelineUnit
:uri-consumer-example: https://github.com/mkobit/jenkins-pipeline-shared-library-example
:uri-build-status-image: https://circleci.com/gh/mkobit/jenkins-pipeline-shared-libraries-gradle-plugin/tree/master.svg?style=svg
:circle-ci-status-badge: image:{uri-build-status-image}["CircleCI", link="https://circleci.com/gh/mkobit/jenkins-pipeline-shared-libraries-gradle-plugin/tree/master"]
:uri-version-badge-image: https://img.shields.io/badge/Gradle%20Plugin-{latest-version}-blue.svg
:uri-gradle-plugin-portal: https://plugins.gradle.org/plugin/com.mkobit.jenkins.pipelines.shared-library
:version-badge: image:{uri-version-badge-image}["Plugin Version", link="{uri-gradle-plugin-portal}"]

{circle-ci-status-badge}
{version-badge}

NOTE: This documentation is for the `HEAD` of the repository.
      To see documentation at a specific version see the link:{uri-github-releases}[GitHub Releases page]

== Purpose

This plugin intends to help with development of link:{uri-jenkins-shared-library-docs}[_Shared Libraries_].

It offers the ability to:

* Unit test using link:{uri-jenkins-pipeline-unit}[_Jenkins Pipeline Unit_]
* Integration test using the link:{uri-jenkins-test-harness}[_Jenkins Test Harness_]
* Code completion in IDE

== Using this plugin

. Consume plugin from Gradle plugin portal (using whatever version you choose)
+
[source, kotlin, subs="attributes+"]
----
plugins {
  id("com.mkobit.jenkins.pipelines.shared-library") version "{latest-version}"
}
----
. Set up test dependencies (like JUnit or Spock)
+
[source, groovy]
----
repositories {
  jcenter()
}

dependencies {
  testImplementation(group: 'junit', name: 'junit', version: '4.12')
}
----
. (Optional) Add dependency to use Jenkins Pipeline Unit for use in unit tests
+
[source, kotlin]
----
sharedLibrary {
  pipelineTestUnitVersion = "1.1"
}
----
. Write some shared library code
+
.src/com/mkobit/LibHelper.groovy
[source, groovy]
----
package com.mkobit

class LibHelper {
  private script
  LibHelper(script) {
    this.script = script
  }

  void sayHelloTo(String name) {
    script.echo("LibHelper says hello to $name!")
  }
}

----
. Write test utilizing `JenkinsRule` that sets up the shared library
+
.test/integration/groovy/com/mkobit/JenkinsGlobalLibraryUsageTest.groovy
[source, groovy]
----
package com.mkobit

import jenkins.plugins.git.GitSCMSource
import org.jenkinsci.plugins.workflow.libs.GlobalLibraries
import org.jenkinsci.plugins.workflow.libs.LibraryConfiguration
import org.jenkinsci.plugins.workflow.libs.LibraryRetriever
import org.jenkinsci.plugins.workflow.libs.SCMSourceRetriever
import org.jenkinsci.plugins.workflow.cps.CpsFlowDefinition
import org.jenkinsci.plugins.workflow.job.WorkflowJob
import org.junit.Before
import org.junit.Rule
import org.junit.Test
import org.jvnet.hudson.test.JenkinsRule


class JenkinsGlobalLibraryUsageTest {

  @Rule
  public JenkinsRule rule = new JenkinsRule()

  @Before
  void configureGlobalGitLibraries() {
    rule.timeout = 30
    final libraryPath = System.getProperty('user.dir')
    println("Using Git library path at $libraryPath")
    final LibraryRetriever retriever = new SCMSourceRetriever(
      new GitSCMSource(
        null,
        libraryPath,
        '',
        'local-source-code',
        // Fetch everything - if this is not used builds fail on Jenkins for some reason
        '*:refs/remotes/origin/*',
        '*',
        '',
        true
      )
    )
    final LibraryConfiguration localLibrary =
      new LibraryConfiguration('pipelineUtilities', retriever)
    localLibrary.implicit = true
    localLibrary.defaultVersion = 'git rev-parse HEAD'.execute().text.trim()
    localLibrary.allowVersionOverride = false
    GlobalLibraries.get().setLibraries(Collections.singletonList(localLibrary))
  }

  @Test
  void testingMyLibrary() {
    final CpsFlowDefinition flow = new CpsFlowDefinition('''
import com.mkobit.LibHelper

final libHelper = new LibHelper(this)
libHelper.sayHelloTo('mkobit')
    ''', true)
    final WorkflowJob workflowJob = rule.createProject(WorkflowJob, 'project')
    workflowJob.definition = flow
    rule.buildAndAssertSuccess(workflowJob)
  }
}
----

== Configuring Versions

The `sharedLibrary` extension can be used to add additional plugin dependencies, Groovy version dependency, Jenkins Core dependency, etc.
As of right now, most of the `workflow`-type plugins are automatically added based on default or configured versions.
See the code for full details, but here is an example of what you can configure:

.build.gradle.kts
[source, kotlin]
----
sharedLibrary {
  groovyVersion = "2.4.12"
  coreVersion = "2.73"
  testHarnessVersion = "2.24"
  pluginDependencies {
    workflowCpsGlobalLibraryPluginVersion = "2.8"
    dependency("io.jenkins.blueocean", "blueocean-web", "1.2.4")
  }
}
----

== Example Consumer

See the link:{uri-consumer-example}[example] for an example of consuming this plugin.
